package querymanager.syntax;
import querymanager.exps.*;

parser code {:
	ErrorMsg.ErrorMsg errorMsg;
	public DBParser(java_cup.runtime.Scanner s, ErrorMsg.ErrorMsg errorMsg){
		super(s);
		this.errorMsg = errorMsg;
	}
:}
scan with {:return getScanner().next_token();:};

terminal	CREATE, UPDATE, ALTER, INSERT, UNIQUE, DELETE, SELECT, DROP, INDEX, CHECK, TRIGGER;
terminal	ASSERT, TABLE, DATABASE, VALUES, INTO, FROM, DISTINCT, LIKE, IN, VIEW, BETWEEN, COUNT;
terminal	GROUP, HAVING, BY, ORDER, SET, SUM, AVG, WHERE, MAX, MIN, ESCAPE, PRIMARY, KEY;
terminal	REFERENCES, JOIN, NOT, NULL, AS, ON, ALL, INTERSECT, EXCEPT, UNION, NATURAL, FULL, SCHEMA;
terminal	OUTER, INNER, IS, CHAR, DATE, INT, VARCHAR, CASCADE, UNKNOWN, TRUE, FALSE, CROSS, TIME;
terminal 	SMALLINT, REAL, FLOAT;
terminal	ANY, CONNECT, EXISTS, ASC, DESC;
terminal	COMMA, COLON, SEMICOLON, LPAREN, RPAREN, DOT, PLUS, MINUS, STAR, DIVIDE, EQ, NEQ, LT, LE, GT, GE, AND, OR;
terminal Integer	NUMBER;
terminal Double		DOUBLENUMBER;
terminal String	STRING, ID;

nonterminal Exp instruction;
nonterminal QueryExp queryExp;
nonterminal SelectFromExp selFrom;
nonterminal TargetExpList attributeList;
nonterminal FromExpList relationList;
nonterminal ConditionExpList conditionList;
nonterminal ConditionExpLists conditionLists;
nonterminal ConditionExp condition;
nonterminal TargetExp attribute;
nonterminal FromExp relation;
nonterminal dropExp, alterExp;
nonterminal Exp.DisOrAll distinctOrAll;
nonterminal String nickName;
nonterminal ParameterExp columnID, fieldID;
nonterminal MathExp mathExp;
nonterminal AggregationExp aggrAttribute;
nonterminal Exp.Aggregation aggregationOp;
nonterminal OptionalExp optionalList;
nonterminal WhereExp whereList;
nonterminal CompareConditionExp compareExp;
nonterminal Exp.CompareOp compareOp;
nonterminal Exp.AnyOrAll anyOrAll;
nonterminal RangeConditionExp rangeExp;
nonterminal InOrNotConditionExp inOrNotExp;
nonterminal ConstantList valueList;
nonterminal LikeConditionExp likeExp;
nonterminal IsNotNullConditionExp nullExp;
nonterminal ExistConditionExp notExistExp;
nonterminal GroupExp groupByList;
nonterminal OrderExp orderByList;
nonterminal AscOeDesc ascOrDesc;
nonterminal InsertExp insertExp;
nonterminal ParameterExpList fieldIDList;
nonterminal UpdateExp updateExp;
nonterminal AssignmentExpList assignmentExpList;
nonterminal AssignmentExp assignmentExp;
nonterminal DeleteExp deleteExp;
nonterminal CreateExp createExp;
nonterminal TableElementExpList createElementExpList;
nonterminal TableElementTypeExp createElementExp;
nonterminal ElementDataType dataType;
nonterminal Exp.Restrict restrictExp;


precedence left SEMICOLON;
precedence left COMMA;
precedence left EXCEPT, UNION, INTERSECT;
precedence left OR;
precedence left AND;
precedence left EQ, NEQ;
precedence nonassoc LT, LE, GT, GE;
precedence left PLUS, MINUS;
precedence left STAR, DIVIDE;
precedence left DOT;

start with instruction;

instruction ::= queryExp:q {:RESULT = q;:}
				| updateExp:u {:RESULT = u;:}
				| createExp:c {:RESULT = c;:}
				| insertExp:i {:RESULT = i;:}
				| deleteExp:d {:RESULT = d;:}
				| alterExp | dropExp;

queryExp ::= selFrom:sf optionalList:op {:RESULT = new QueryExp(sf, op);:}
				| LPAREN queryExp:qe RPAREN {:RESULT = qe;:}
				| queryExp:q1 EXCEPT queryExp:q2 {:RESULT = new SetQueryExp(q1, q2, SetQueryExp.EXCEPT);:}
				| queryExp:q1 UNION queryExp:q2 {:RESULT = new SetQueryExp(q1, q2, SetQueryExp.UNION);:}
				| queryExp:q1 INTERSECT queryExp:q2 {:RESULT = new SetQueryExp(q1, q2, SetQueryExp.INTERSECT);:}
				;
selFrom ::= SELECT distinctOrAll:d attributeList:at FROM relationList:re
			{:RESULT = new SelectFromExp(d, at, re);:}
			;

distinctOrAll ::= DISTINCT {:RESULT = Exp.DisOrAll.DISTINCT;:}
				| ALL {:RESULT = Exp.DisOrAll.ALL;:}
				|
				;

attributeList ::= attribute:a {:RESULT = new TargetExpList(a, null);:}
				| attribute:a COMMA attributeList:al {:RESULT = new TargetExpList(a, al);:}
				;
nickName ::= ID:i {:RESULT = i;:}
				| AS ID:i {:RESULT = i;:}
				|;

attribute ::= columnID:c nickName:n {:RESULT = new ParameterTargetExp(c, n);:}
				| mathExp:m nickName:n {:RESULT = new ExpressionTargetExp(m, n);:}
				| aggrAttribute:a nickName:n {:RESULT = new AggregationTargetExp(a, n);:}
				| NUMBER:n {:RESULT = new ConstantTargetExp(new Constant(n));:}
				;

columnID ::= fieldID:f {:RESULT = f;:}
				| STAR {:RESULT = new ParameterExp(null, ParameterExp.STAR);:}
				| ID:i DOT STAR {:RESULT = new ParameterExp(i, ParameterExp.STAR);:}
				;

fieldID ::= ID:i {:RESULT = new ParameterExp(null, i);:}
				| ID:i1 DOT ID:i2 {:RESULT = new ParameterExp(i1, i2);:}
				;

mathExp ::= attribute:a1 PLUS attribute:a2 {:RESULT = new MathExp(a1, a2, Exp.MathOp.PLUS);:}
				| attribute:a1 MINUS attribute:a2 {:RESULT = new MathExp(a1, a2, Exp.MathOp.MINUS);:}
				| attribute:a1 STAR attribute:a2 {:RESULT = new MathExp(a1, a2, Exp.MathOp.TIMES);:}
				| attribute:a1 DIVIDE attribute:a2 {:RESULT = new MathExp(a1, a2, Exp.MathOp.DIVIDE);:}
				;

aggrAttribute ::= aggregationOp:ao LPAREN distinctOrAll:d columnID:c RPAREN
				{:RESULT = new AggregationExp(ao, d, c);:}
				;
aggregationOp ::= COUNT {:RESULT = Exp.Aggregation.COUNT;:}
				| SUM {:RESULT = Exp.Aggregation.SUM;:}
				| AVG {:RESULT = Exp.Aggregation.AVG;:}
				| MIN {:RESULT = Exp.Aggregation.MIN;:}
				| MAX {:RESULT = Exp.Aggregation.MAX;:}
				;

relationList ::= relation:r {:RESULT = new FromExpList(r, null);:}
				| relation:r COMMA relationList:rl {:RESULT = new FromExpList(r, rl);:}
				;

relation ::= ID:i {:RESULT = new FromExp(i, null, null);:}
				| ID:i1 ID:i2 {:RESULT = new FromExp(i1, i2, null);:}
				|  LPAREN queryExp:q RPAREN {:RESULT = new FromExp(null, null, q);:}
				| LPAREN queryExp:q RPAREN ID:i {:RESULT = new FromExp(null, i, q);:}
				;

optionalList ::=  whereList:w groupByList:g orderByList:o
				{:RESULT = new OptionalExp(w,g,o);:}
				;

whereList ::= WHERE conditionLists:cls {:RESULT = new WhereExp(cls);:}
				| ;

conditionLists ::= conditionList:cl {:RESULT = new ConditionExpLists(cl, null);:}
				| conditionList:cl OR conditionLists:cls {:RESULT = new ConditionExpLists(cl,cls);:}
				;

conditionList ::= condition:c {:RESULT = new ConditionExpList(c,null);:}
				| condition:c AND conditionList:cl {:RESULT = new ConditionExpList(c, cl);:}
				;

condition ::= compareExp:c {:RESULT = c;:}
				| rangeExp:r {:RESULT = r;:}
				| inOrNotExp:i {:RESULT = i;:}
				| likeExp:l {:RESULT = l;:}
				| nullExp:n {:RESULT = n;:}
				| notExistExp:n {:RESULT = n;:}
				;

compareExp ::= fieldID:f1 compareOp:c fieldID:f2 {:RESULT = new ParameterCompareConditionExp(f1,c,f2);:}
				| fieldID:f compareOp:c NUMBER:n {:RESULT = new ConstantCompareConditionExp(f,c,new Constant(n));:}
				| fieldID:f compareOp:c STRING:s {:RESULT = new ConstantCompareConditionExp(f,c,new Constant(s));:}
				| fieldID:f compareOp:c ANY queryExp:q {:RESULT = new QueryCompareConditionExp(f,c,q,Exp.AnyOrAll.ANY);:}
				| fieldID:f compareOp:c ALL queryExp:q {:RESULT = new QueryCompareConditionExp(f,c,q,Exp.AnyOrAll.ALL);:}
				| fieldID:f compareOp:c queryExp:q {:RESULT = new QueryCompareConditionExp(f,c,q,null);:}
				;
compareOp ::= EQ {:RESULT = Exp.CompareOp.EQ;:}
				| NEQ {:RESULT = Exp.CompareOp.NEQ;:}
				| LT {:RESULT = Exp.CompareOp.LT;:}
				| LE {:RESULT = Exp.CompareOp.LE;:}
				| GT {:RESULT = Exp.CompareOp.GT;:}
				| GE {:RESULT = Exp.CompareOp.GE;:}
				;

rangeExp ::= fieldID:f1 BETWEEN fieldID:f2 AND fieldID:f3 {:RESULT = new ParameterRangeConditionExp(f1,f2,f3,false);:}
				| fieldID:f BETWEEN NUMBER:n1 AND NUMBER:n2 {:RESULT = new ConstantRangeConditionExp(f,new Constant(n1), new Constant(n2), false);:}
				| fieldID:f BETWEEN STRING:s1 AND STRING:s2  {:RESULT = new ConstantRangeConditionExp(f,new Constant(s1), new Constant(s2), false);:}
				| fieldID:f BETWEEN queryExp:q1 AND queryExp:q2 {:RESULT = new QueryRangeConditionExp(f, q1, q2, false);:}
				| fieldID:f1 NOT BETWEEN fieldID:f2 AND fieldID:f3 {:RESULT = new ParameterRangeConditionExp(f1,f2,f3,true);:}
				| fieldID:f NOT BETWEEN NUMBER:n1 AND NUMBER:n2 {:RESULT = new ConstantRangeConditionExp(f,new Constant(n1), new Constant(n2), true);:}
				| fieldID:f NOT BETWEEN STRING:s1 AND STRING:s2  {:RESULT = new ConstantRangeConditionExp(f,new Constant(s1), new Constant(s2), true);:}
				| fieldID:f NOT BETWEEN queryExp:q1 AND queryExp:q2 {:RESULT = new QueryRangeConditionExp(f, q1, q2, true);:}
				;

inOrNotExp ::= fieldID:f IN LPAREN valueList:vl RPAREN {:RESULT = new ValueListInOrNotConditionExp(f, vl, false);:}
				| fieldID:f NOT IN LPAREN valueList:vl RPAREN {:RESULT = new ValueListInOrNotConditionExp(f, vl, true);:}
				| fieldID:f IN queryExp:q {:RESULT = new QueryInOrNotConditionExp(f,q,false);:}
				| fieldID:f NOT IN queryExp:q {:RESULT = new QueryInOrNotConditionExp(f,q,true);:}
				;
valueList ::= NUMBER:n {:RESULT = new ConstantList(new Constant(n), null);:}
				| STRING:s {:RESULT = new ConstantList(new Constant(s), null);:}
				| STRING:s COMMA valueList:vl {:RESULT = new ConstantList(new Constant(s), vl);:}
				| NUMBER:n COMMA valueList:vl {:RESULT = new ConstantList(new Constant(n), vl);:}
				;

likeExp ::= fieldID:f LIKE STRING:s {:RESULT = new LikeConditionExp(f,new MatchPattern(s), false);:}
  				| fieldID:f NOT LIKE STRING:s {:RESULT = new LikeConditionExp(f,new MatchPattern(s), true);:}
				;

nullExp ::= fieldID:f IS NULL {:RESULT = new IsNotNullConditionExp(f, false);:}
				| fieldID:f IS NOT NULL {:RESULT = new IsNotNullConditionExp(f, true);:}
				;

notExistExp ::= EXISTS queryExp:q {:RESULT = new ExistConditionExp(false, q);:}
				| NOT EXISTS queryExp:q {:RESULT = new ExistConditionExp(true, q);:}
				;

groupByList ::= GROUP BY fieldID:f {:RESULT = new GroupExp(f, null);:}
				| GROUP BY fieldID:f HAVING conditionLists:cls {:RESULT = new GroupExp(f, cls);:}
				|;

orderByList ::= ORDER BY fieldID:f {:RESULT = new OrderExp(f, null);:}
				| ORDER BY fieldID:f ASC {:RESULT = new OrderExp(f, Exp.AscOrDesc.ASC);:}
				| ORDER BY fieldID:f DESC {:RESULT = new OrderExp(f, Exp.AscOrDesc.DESC);:}
				| ;

insertExp ::= INSERT INTO ID:i VALUES LPAREN valueList:vl RPAREN {:RESULT = new ConstantInsertExp(i, null, vl);:}
				| INSERT INTO ID:i LPAREN fieldIDList:fl RPAREN VALUES LPAREN valueList:vl RPAREN {:RESULT = new ConstantInsertExp(i, fl, vl);:}
				| INSERT INTO ID:i VALUES queryExp:q {:RESULT = new QueryInsertExp(i, null, q);:}
				| INSERT INTO ID:i LPAREN fieldIDList:fl RPAREN VALUES queryExp:q {:RESULT = new QueryInsertExp(i, fl, q);:}
				;
fieldIDList ::= fieldID:f {:RESULT = new ParameterExpList(f, null);:}
				| fieldID:f COMMA fieldIDList:fl {:RESULT = new ParameterExpList(f, fl);:}
				;
updateExp ::= UPDATE ID:i SET assignmentExpList:al whereList:wl {:RESULT = new UpdateExp(i, al, wl);:}
				;

assignmentExpList ::= assignmentExp:a {:RESULT = new AssignmentExpList(a, null);:}
				| assignmentExp:a COMMA assignmentExpList:al {:RESULT = new AssignmentExpList(a, al);:}
				;

assignmentExp ::= fieldID:f EQ NUMBER:n {:RESULT = new ConstantAssignmentExp(f, new Constant(n));:}
				| fieldID:f EQ STRING:s {:RESULT = new ConstantAssignmentExp(f, new Constant(s));:}
				| fieldID:f EQ mathExp:m {:RESULT = new ExpressionAssignmentExp(f, m);:}
				;

deleteExp ::= DELETE FROM relation:r whereList:wl {:RESULT = new DeleteExp(r, wl);:}
				;

createExp ::= CREATE TABLE ID:i LPAREN createElementExpList:cl RPAREN {:RESULT = new CreateExp(i,cl);:}
				;

createElementExpList ::= createElementExp:c {:RESULT = new TableElementExpList(c,null);:}
				| createElementExp:c COMMA createElementExpList:cl {:RESULT = new TableElementExpList(c, cl);:}
				;

createElementExp ::= ID:i dataType:dt restrictExp:r {:RESULT = new TableElementTypeExp(i, dt, r);:}
				;

dataType ::= SMALLINT {:RESULT = new ElementDataType(Exp.DataType.SMALLINT, 0);:}
				| CHAR LPAREN NUMBER:n RPAREN {:RESULT = new ElementDataType(Exp.DataType.CHAR, n);:}
				| VARCHAR LPAREN NUMBER:n RPAREN {:RESULT = new ElementDataType(Exp.DataType.VARCHAR, n);:}
				| INT {:RESULT = new ElementDataType(Exp.DataType.INT, 0);:}
				| REAL {:RESULT = new ElementDataType(Exp.DataType.REAL, 0);:}
				| FLOAT LPAREN NUMBER:n RPAREN {:RESULT = new ElementDataType(Exp.DataType.FLOAT, n);:}
				| DATE {:RESULT = new ElementDataType(Exp.DataType.DATE, 0);:}
				| TIME {:RESULT = new ElementDataType(Exp.DataType.TIME, 0);:}
				;

restrictExp ::=
				| UNIQUE {:RESULT = Exp.Restrict.UNIQUE;:}
				| PRIMARY KEY {:RESULT = Exp.Restrict.PRIMARYKEY;:}
				;
